// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.android_gradle_version = "4.1.3"
    ext.kotlin_version = "1.4.10"
    ext.dektekt_version = "1.16.0"
    ext.junit_5_version = "1.7.1.1"
    ext.jacoco_version = "0.8.6"

    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$dektekt_version"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:$junit_5_version"
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "org.sonarqube" version "3.1.1"
    id "maven-publish"
}

apply from: './dependencies.gradle'

sonarqube {
    properties {
        property "sonar.projectKey", sonarqubeSonarProjectKey
        property "sonar.organization", sonarqubeSonarOrganization
        property "sonar.host.url", sonarqubeSonarHostUrl
        property("sonar.sourceEncoding", sonarqubeSonarSourceEncoding)
        property("sonar.java.coveragePlugin", sonarqubeSonarCoveragePlugin)
        property("sonar.coverage.exclusions", sonarqubeSonarCoverageExclusions)
        property("sonar.coverage.jacoco.xmlReportPaths", sonarqubeSonarCoverageJacocoXMLReportPaths)
    }
}

allprojects {
    group="com.ygorxkharo.obey"
    version="1.0.0-SNAPSHOT"
    repositories {
        jcenter()
        google()
    }
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        toolVersion = versions.detekt
        buildUponDefaultConfig = true
        config = files("$rootDir/config/detekt/custom-detekt-config.yml")
    }

    afterEvaluate { project ->
        project.apply from: '../jacoco.gradle'
        project.apply from: '../library-publish.gradle'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}